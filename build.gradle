import org.gradle.api.tasks.testing.Test

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.4'
}

group 'com.rundeck.plugin'

apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8
ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='com.rundeck.plugin.HttpNotificationPlugin'
ext.pluginName = 'Http Notification'
ext.pluginDescription = 'A notification plugin that makes HTTP requests'


scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation{
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }

}

dependencies {

    implementation group: 'org.rundeck', name: 'rundeck-core', version: '4.3.+'

    pluginLibs 'com.github.rundeck-plugins:http-step:RUN-1770-SNAPSHOT'
    pluginLibs group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    pluginLibs group: 'com.google.code.gson', name: 'gson', version:'2.10.1'
    pluginLibs group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version:'1.13'

    implementation 'org.slf4j:slf4j-api:2.0.7'

    implementation 'org.codehaus.groovy:groovy-all:3.0.17'

    testImplementation group: 'junit', name: 'junit', version: '4.12'

    testImplementation "org.codehaus.groovy:groovy-all:3.0.17"
    testImplementation "org.spockframework:spock-core:2.0-groovy-3.0"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation 'org.objenesis:objenesis:1.4'

}


// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}



jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.10.1+'
        attributes 'Rundeck-Plugin-Tags': 'java,notification'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/http-notification'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}


//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

wrapper {
    gradleVersion = '4.4.1'
}

tasks.withType(Test) {
    useJUnitPlatform()
}